plugins {
    id 'java-library'
    id 'checkstyle'
}

checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}

checkstyleMain {
    //source has to be pointed at src/main/java, thats why it is a List
    source = List.of('intelij-plugin-1-1/src/main/java', 'rfc-parser/src/main/java', 'intelij-plugin-1-1/src/main/resources')
}

checkstyleTest {
    source = List.of('intelij-plugin-1-1/src/test/java', 'rfc-parser/src/test/java')
}

group 'tech.pantheon.YANGinator'
version '1.6-SNAPSHOT'

allprojects {
    apply plugin: 'java'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenCentral()
    }

}

subprojects {
    version = rootProject.version
    group = rootProject.group
}

tasks {
    wrapper {
        gradleVersion = "$gradleVersion"
    }
}

task createReleaseVersion {
    doLast {
        def snapshotVersion = buildFile.getText().find(version)
        def releaseVersion = snapshotVersion.replace("-SNAPSHOT", "")
        println "Release version : " + releaseVersion
        String newBuildFile = buildFile.getText().replaceFirst("version '$version'", "version '" + releaseVersion + "'")
        buildFile.setText(newBuildFile)
    }
}

task createNextSnapshotVersion {
    doLast {
        def releaseVersion = buildFile.getText().find(version)
        String currentMinor = version.substring(releaseVersion.lastIndexOf('.') + 1)
        int nextMinor = currentMinor.toInteger() + 1
        def snapshotVersion = releaseVersion.replace("." + currentMinor, "." + nextMinor + "-SNAPSHOT")
        println "Snapshot version : " + snapshotVersion
        String newBuildFile = buildFile.getText().replaceFirst("version '$version'", "version '" + snapshotVersion + "'")
        buildFile.setText(newBuildFile)
    }

}
