import org.jetbrains.intellij.platform.gradle.TestFrameworkType
import org.jetbrains.intellij.platform.gradle.IntelliJPlatformType

plugins {

    id 'org.jetbrains.intellij.platform' version "$intellijGradlePluginVersion"
    id 'org.jetbrains.changelog' version "$changelogVersion"
}

intellijPlatform {
    pluginVerification {
        ides {
            ide(IntelliJPlatformType.IntellijIdeaCommunity, "2024.3.4.1")
            ide(IntelliJPlatformType.IntellijIdeaUltimate, "2024.3.5")
            ide (IntelliJPlatformType.PyCharmCommunity,"2024.3.5")
            ide (IntelliJPlatformType.PyCharmProfessional,"2024.3.5")
            ide (IntelliJPlatformType.CLion,"2024.3.5")
            ide (IntelliJPlatformType.GoLand,"2024.3.5")
            recommended()
        }
    }
}

apply plugin: 'org.jetbrains.changelog'

changelog {
    path = "${project.parent.projectDir}/CHANGELOG.md"
    header = "${-> version.get()}"
    headerParserRegex = ~/(\d+\.\d+)/
    itemPrefix = "*"
    keepUnreleasedSection = true
    unreleasedTerm = "[Unreleased]"
    groups = []
}

tasks {
    patchPluginXml {
        sinceBuild = '213'
        untilBuild = '251.*'
        changeNotes = changelog.getLatest().toHTML() + "<a href=\"https://github.com/PANTHEONtech/YANGinator/blob/master/CHANGELOG.md\">more...</a>"
    }
}

runIde {
    jvmArgs("-Xmx2000m")
}

publishPlugin {
    token.set(System.getenv("PUBLISH_TOKEN"));
    channels = [System.getenv("CHANNELS")]
}

test {
    systemProperty "idea.home.path", "src/test"
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/gen'
        }
    }
}

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity ideaVersion
        bundledPlugin ("com.intellij.java")
        testFramework TestFrameworkType.Platform.INSTANCE
    }

    testImplementation group: 'org.junit.jupiter',
            name: 'junit-jupiter-engine', version: jupiterEngineVersion
}