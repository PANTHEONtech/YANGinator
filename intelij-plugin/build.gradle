plugins {
    id 'org.jetbrains.intellij' version "$intellijGradlePluginVersion"
    id 'org.jetbrains.changelog' version "$changelogVersion"
}

apply plugin: 'org.jetbrains.changelog'

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = ideaVersion
    plugins = ['com.intellij.java']
}

changelog {
    path = "${project.projectDir}/CHANGELOG.md"
    header = "${-> version.get()}"
    headerParserRegex = ~/(\d+\.\d+)/
    itemPrefix = "*"
    keepUnreleasedSection = true
    unreleasedTerm = "[Unreleased]"
    groups = []
}

tasks {
    patchPluginXml {
        sinceBuild = '211'
        untilBuild = ''
        changeNotes = changelog.getLatest().toHTML() + "<a href=\"https://github.com/PANTHEONtech/YANGinator/blob/master/intelij-plugin/CHANGELOG.md\">more...</a>"
    }
}

publishPlugin {
    token.set(System.getenv("PUBLISH_TOKEN"))
}

test {
    systemProperty "idea.home.path", "src/test"
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/gen', 'src/main/highlighter'
        }
    }
}

dependencies {
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jupiterEngineVersion
}

runPluginVerifier {
    File file = new File("intelij-plugin/compatible.version")
    def line
    ideVersions = []
    file.withReader { reader ->
        while ((line = reader.readLine()) != null) {
            if (!line.isEmpty() && !line.contains('#')) {
                ideVersions.add(line)
            }
        }
    }
}