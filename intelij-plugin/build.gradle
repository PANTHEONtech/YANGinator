plugins {
    id 'org.jetbrains.intellij' version "$intellijGradlePluginVersion"
    id 'org.jetbrains.changelog' version "$changelogVersion"
}

apply plugin: 'org.jetbrains.changelog'

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = ideaVersion
    plugins = ['com.intellij.java']
}

changelog {
    path = "${project.parent.projectDir}/CHANGELOG.md"
    header = "${-> version.get()}"
    headerParserRegex = ~/(\d+\.\d+)/
    itemPrefix = "*"
    keepUnreleasedSection = true
    unreleasedTerm = "[Unreleased]"
    groups = []
}

tasks {
    patchPluginXml {
        sinceBuild = '211'
        untilBuild = ''
        changeNotes = changelog.getLatest().toHTML() + "<a href=\"https://github.com/PANTHEONtech/YANGinator/blob/master/CHANGELOG.md\">more...</a>"
    }
}

publishPlugin {
    token.set(System.getenv("PUBLISH_TOKEN"))
}

test {
    systemProperty "idea.home.path", "src/test"
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/gen', 'src/main/highlighter'
        }
    }
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jupiterEngineVersion
}

// Runs the IntelliJ Plugin Verifier tool to check the binary compatibility with explicitly specified IntelliJ IDE builds in compatible.version file
runPluginVerifier {
    File file = new File("${project.parent.projectDir}/COMPATIBLE.version")
    def line
    ideVersions = []
    file.withReader { reader ->
        while ((line = reader.readLine()) != null) {
            if (!line.isEmpty() && !line.contains('#')) {
                ideVersions.add(line)
            }
        }
    }
}
